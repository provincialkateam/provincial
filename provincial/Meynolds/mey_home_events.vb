! принудительно выводим шкаф для комнаты Катии гостевой, если ГГ ночует в доме до получения ключа
if $args[0] = 'go_out_no_key':
	act '<b>Уйти</b>':
		if current_clothing < 3:
			*clr & cla
			gs 'zz_render','','images/clothing/tanga.jpg','<red>Вам надо одеться!</red>'
			act 'OK': gt 'loker'
			exit
		else
			minut += rand(10,15)
			gt 'gorodok'
		end
	end
end
! никого нет дома
if $args[0] = 'no_home':
	*clr & cla
	gs 'zz_render','','pavlovo/meynolds/tamara/meeting/tea0', iif(hour < 10 or hour > 20,$_str[0],$_str[1]+$_str[2])
	killvar '$_str'
	act 'Уйти':
		minut += 10
		gs 'zz_render','','','Вы уходите в город, так как никого нет дома.'
		gt 'gorodok'
	end
end
if $args[0] = 'clean_home':
	act 'Убирать в доме':
		cla
		$npc['0,mey_help_day'] = day
		$npc['0,mey_help_count'] += 1
		minut += rand(50,70)
		sweat += 3
		manna -= 5
		gs 'stat'
		if $npc['15,bath_qw'] = 0 and $npc['15,bath_qw_day'] ! day:
			gs 'mey_vika_events', 'bath_qw0'
		else
			gs 'zz_render','','','<br>Вы кое-как вытащили из кладовки здоровенный и тяжеленный моющий пылесос, и в течение часа наводили марафет в доме.'
			act 'Закончить': gt 'mey_home', 0
		end
	end
end
if $args[0] = 'cooking':
	act 'Готовить на всех':
		cla
		$npc['0,mey_help_day'] = day
		$npc['0,mey_help_count'] += 1
		minut += rand(50,70)
		gs 'stat'
		gs 'zz_render','','','<br>Вы, достав продукты из холодильника, час возились на кухне, готовя на всё семейство.'
		act 'Закончить': gt 'mey_home', 0
	end
end
if $args[0] = 'fireplace':
	*clr & cla
	gs 'zz_render','','pavlovo/meynolds/home/common/fireplace.jpg','Вы разожгли камин, и сев поближе, завороженно глядите на танец огненных лепестков под уютное потрескивание поленьев.'
	if manna < 30 or son < 8:
		i = rand(180,240)
		minut += i
		manna += rand(20,30)
		son += i/20
		gs 'zz_render','','','Как и стоило ожидать, вы задремали и проспали больше трёх часов.'
		if month > 9 or month < 5: gs 'zz_render','','','Кто-то даже немножко позаботился о вас, укрыв теплым клетчатым пледом.'
	else
		manna += rand(10,20)
		minut += rand(30,60)
		gs 'zz_render','','','Немного помечтав, вы с большой неохотой все же решаете вернуться к делам насущным.'
	end
	gs 'stat'
	act '...': gt 'mey_home', 4
end
! -- courtyard
if $args[0] = 'redress':
	act 'Переодеться':
		cla
		if current_clothing > 2:
			act 'Снять все':
				cla
				minut += 5
				flag_tanga = tanga
				tanga = 0
				gs 'zz_clothing', 'redress', 0
				gs 'zz_render','','','Уютный дворик скрыт от посторонних глаз, так что без толики стеснения вы раздеваетесь догола - тут стесняться некого.'
				act '...': gt 'mey_home', 9
			end
			gs 'mey_home_events','redress_swimsuit'
		else
			if current_clothing ! 2: gs 'mey_home_events','redress_swimsuit'
			act 'Одеться': gs 'mey_home_events','cloth_on'
		end
	end
end
if $args[0] = 'redress_swimsuit':
	act 'Надеть купальник':
		cla
		minut += 5
		gs 'zz_clothing', 'redress', 2
		flag_tanga = tanga
		tanga = 0
		gs 'zz_render','','','Хоть уютный дворик и закрыт от посторонних глаз, но вы всё же решили надеть купальник.'
		act '...': gt 'mey_home', 9
	end
end
if $args[0] = 'cloth_on':
	cla
	minut += 5
	gs 'zz_clothing', 'redress', prev_clothing
	tanga = flag_tanga
	prev_clothing = -1
	gs 'zz_render','','','Вы переоделись в свою одежду.'
	act '...': gt 'mey_home', 9
end
if $args[0] = 'swim':
	act 'Купаться (20 мин)':
		*clr & cla
		minut += 20
		gs 'stat'
		gs 'zz_render','','pavlovo/meynolds/home/common/swimming'+iif(current_clothing=0,'_naked','')+rand(0,1)+'.jpg','Вы по лесенке забрались через высокий бортик в бассейн. Он круглый, метров восемь в диаметре и довольно глубокий, метра два в самом глубоком месте - так что можно и поплавать, и понырять от души! Вода в нём всегда тёплая: в холодное время года включается подогрев.'
		act 'Вылезти с бассейна': gt 'mey_home', 9
	end
end
if $args[0] = 'sunbathe':
	act 'Загорать (60 мин)':
		*clr & cla
		minut += 60
		gs 'stat'
		gs 'zz_render','','pavlovo/meynolds/home/common/sunbathe'+iif(current_clothing=0,'_naked','')+rand(0,1)+'.jpg','Вы с комфортом устроились в шезлонге у небольшого сборного бассейна, нежась на солнышке...'
		act 'Закончить': gt 'mey_home', 9
	end
end
! ивент с порнушкой ТМ
if $args[0] = 'dirt':
	! mey_tamara_dirt_qw:
	! 0 - не начат
	! 1 - ГГ посмотрела только один диск
	! 2 - ГГ просмотрела оба диска, ивент завершен
	! 10 - ГГ не смотрела дисков, ивент провален
	gs 'zz_render','','','<br><br>На полочке у огромного экрана вы замечаете несколько дисков в простых прозрачных боксах. Обычные “самописки” которыми давно никто уже не пользуется, на которых маркером накарябаны какие-то цифры - похоже, даты.'
	act 'Вставить диск в плеер':
		*clr & cla
		gs 'zz_render','','pavlovo/meynolds/tamara/dirt/0.gif','Ну как тут удержаться? Вы вставляете один из этих дисков в плеер.<br>На экране возникает самая обычная порнуха из той, которую нынче относят к категории “vintage” - родом из начала девяностых. Вы вытаскиваете диск из плеера и вставляете другой. И здесь тоже старый порнофильм. Лениво промотав чуть-чуть, вы уже хотели было выключить, но вдруг у вас падает челюсть, и вы принимаетесь отматывать назад и смотрите теперь очень внимательно. “Да ну! Показалось!” - терзают вас сомнения: в кадре часто мелькает ярко-рыжая молоденькая девушка, очень похожая на вашу наставницу.'
		act 'Вставить второй диск':
			*clr & cla
			$npc['45,dirt_qw'] = 2
			gs 'zz_render','','pavlovo/meynolds/tamara/dirt/1.gif','Вы вставляете следующий диск, теперь уже быстро проглядывая его на ускоренной перемотке и высматривая эту девушку. Точно, и здесь она присутствует. И на следующем тоже. Просмотр порно на ускоренной перемотке быстро вызывает у вас истерический смех: актёры и актрисы забавно дёргаются и пищат тонкими голосками - на этот лилипутский цирк без слёз смотреть невозможно!<br>Вам кажется, что холле послышались какие-то звуки. Вы быстренько возвращаете диски на место, и, выключив аппаратуру, хватаете книжку, раскрыв её на случайной странице, и делаете постную морду, имитируя, что погружены в чтение.'
			act 'Закончить просмотр':
				*clr & cla
				gs 'zz_render','','pavlovo/meynolds/tamara/dirt/2.jpg','Но всё-таки червячок сомнения гложет - как в мультфильме “Остров сокровищ” сквайра Треллони: “Так я пгав или не пгав?” - уж очень рыжуха на видео и фото похожа  на Тамару Михайловну! Только молоденькую, тоненькую, да и сиськи у актрисы кажутся поменьше выдающихся “достоинств” Тётьтомы. Да и фильмы все - всякие наказания со связываниями, что совсем не вяжется с доброй, мягкой, но при этом сильной и властной мамой рыжих близняшек. И спросить “в лоб” не спросишь! Вы решаете пока запомнить - ибо всё тайное когда-нибудь становится явным.'
				act 'Надо запомнить...':
					minut += rand(20,40)
					gt 'mey_home', 4
				end
			end
		end
		act 'Не, хватит!':
			minut += rand(10,20)
			$npc['45,dirt_qw'] = 1
			gt 'mey_home', 4
		end
	end
	act 'Пускай себе лежат':
		minut += 1
		$npc['45,dirt_qw'] = 10
		gt 'mey_home', 4
	end
end