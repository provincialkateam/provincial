if $ARGS[0] = 'get_skates':
	*clr & cla
	minut += 5
	skates_cloth = 1
	gs 'zz_render', $_str, 'common/ice_rink/get_skates_<<$loc>>.jpg','Вы сняли свою обувь, и надели коньки. Теперь, можно пойти кататься.'
	act'Назад': gt $loc, $metka
end
if $ARGS[0] = 'off_skates':
	*clr & cla
	minut += 10
	skates_cloth = 0
	gs 'zz_render', $_str, 'common/ice_rink/off_skates_<<$loc>>.jpg','Вы сняли коньки и надели свою обувь. Странно, но коньки оказалось тяжелей снять, чем надеть.'
	act'Назад': gt $loc, $metka
end
if $args[0] = 'rink_start':
	*clr & cla
	minut += 5
	$_str = 'Каток'
	gs'stat'
	gs 'zz_render', $_str, 'common/ice_rink/rink_start_<<$loc>>.jpg','Вы аккуратно вышли на каток. На нем уже катаются много людей. Кто-то уже неплохо управляется с коньками, летая по всему озеру, а кто-то не может и метра проехать без падения.'
	act'Кататься': gt 'ice_rink','to_skate'
	act'Уйти': gt $loc, $metka
end
if $args[0] = 'to_skate':
	*clr & cla
	if skateday ! day:skateday = day & body['day_weight']-=1
	sweat += 1
	if speed < 100: speed += 1
	if agil < 70: agil += 1
	if react < 70 :react += 1
	if skates_stat < 100: skates_stat += 1
	minut += 30
	i = rand(1,3)
	gs'stat'
	if skates_stat < 10:
		manna -= 10
		gs 'zz_render', $_str, 'common/ice_rink/to_skate_<<$loc>>'+rand(1,3)+'.jpg', 'Вы пытаетесь проехаться на коньках, но у вас ничего не получается. Вы то и дело падаете, и едете уже не на коньках, а на попе.**Некоторые, из катающихся рядом людей вас подбадривают, другие смеются над вами.'
	elseif skates_stat < 30:
		manna -= 5
		if i = 1: skates_stat += 1
		gs 'zz_render', $_str, 'common/ice_rink/to_skate_<<$loc>>'+iif(i = 1,9,rand(1,6))+'.jpg','Ваши постоянные попытки принесли свои плоды. Вы уже не так часто падаете, хоть это и происходит регулярно. Вы даже начинаете получать от катания удовольствие.'+iif(i = 1,'Катаясь, вы внезапно осознаете, как именно нужно выполнять определенное движение. Это определенно повысит ваши навыки.','')
	elseif skates_stat < 50:
		manna += 5
		if i = 1: skates_stat += 2
		gs 'zz_render', $_str, 'common/ice_rink/to_skate_<<$loc>>'+iif(i = 1,'',rand(4,9))+'.jpg', iif(i = 1,'Наблюдая за тем, как вы очередной раз упали, один из более опытных конькобежцев подошел к вам, и объяснил вашу ошибку. С его помощью, вы наконец поняли, как больше её не повторять.','Вы наконец проехали сотню метров ни разу не упав, при этом. Это определенно прогресс. Люди, так же замечают ваши успехи, и поздравляют вас.')+'Вы продолжаете кататься, меньше задумываясь над тем, чтобы не упасть. И больше получая удовольтвие.'
	elseif skates_stat < 70:
		manna += 10
		if i = 1: skates_stat += 3
		gs 'zz_render', $_str, 'to_skate_<<$loc>>'+iif(i = 1,15,rand(7,12))+'.jpg','У вас получается все лучше и лучше. Вы практически не падаете. Все больше наслаждаетесь скольжению на коньках, ощущая поток ветра на своем лице.'+iif(i = 1,'Катаясь, вы наблюдали за более опытными товарищами, стараясь им подражать, ваши теперь навыки несомненно возрастут.','')
	else
		manna += 15
		gs 'zz_render', $_str, 'common/ice_rink/to_skate_<<$loc>>'+rand(13,15)+'.jpg','Вы в совершенстве овладели мастерством скольжения. Теперь, вы больше не упадете. Вы научились на своих ошибках, немного с чужой помощью, немного из наблюдений.**Результат превзошёл ваши ожидания. Возможно, вы могли бы профессионально заняться фигурным катанием.'
	end
	if hour <= func('zz_weather','sunset'):
		act'Передохнуть': gt 'ice_rink','rink_start'
	else
		gs 'zz_render', '', '','Время слишком позднее. Каток закрывается, люди расходятся по домам. И вам тоже пора.'
	end
	act'Уйти': gt $loc, $metka
end