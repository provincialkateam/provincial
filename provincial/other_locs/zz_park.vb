$loc = $curloc
if $args[0] = '' or $args[0] = 'main':
	*clr & cla
	$metka = $args[0]
	gs 'time'
	gs 'stat'
	! -- ДЕЙСТВИЯ -- !
	! точки выхода с парка
	gs $curloc,'finish'
	! стандартные действия в парке
	if hour > func('zz_weather','sunrise') and hour <= func('zz_weather','sunset') and sunWeather >= 0: gs $curloc,'actions'
	! -- ОПИСАНИЕ -- !
	gs 'zz_render','Парк',func('zz_funcs','mk_image_ex','common/park/park',1),'Обширный городской парк. У входа "окультурено", но большая часть парка - полудикая, изрезанная сетью дорожек и тропинок. Ночью здесь может быть опасно.'
	! насильник
	if rand(0,100) <= 1 and (hour >= 22 or hour <= 4) and $npc['0,rapist_month'] ! month: gt $curloc,'rapist'
	! вор/грабитель - чем больше налички - тем больше вероятность
	if rand(0,100) <= money/1000 and $npc['0,theft_month'] ! month:
		if hour >= func('zz_weather','sunrise') and hour <= func('zz_weather','sunset'):
			gt $curloc,'theft'
		elseif hour >= func('zz_weather','sunset') - 3:
			gt $curloc,'robber'
		end
	end
	! особенности для парка в городке и сити
	if $control_point = 'gorodok':
		! трио с Катей
		if hour = 17 and minut <= 20 and $npc['14,qwKatya'] = 72 and $npc['14,qwKatya_trio_day'] = daystart and month > 3 and month < 11 and sunWeather >= 0: act 'Идти на трио с Катей': gt 'mey_katya_events','park_trio'
		! здесь может звонить бойфренд
		gs 'zz_phone','boyfriend_call_init'
		!--
		if hour >= 10 and hour <= 18: gs 'zz_render','','','Здесь находится небольшой <a href="exec:gt''cinema'',''main'',1">кинотеатр</a>.'
		gs 'zz_render','','','Кафешка <a href="exec:gt''cafe_parco'',''start''">"Del Parco"</a> из-за своего броского внешнего вида выглядит в парке как не из этого мира.'
		! вход ветки Рекса
		if rexPark = 1 and rexParkDay = daystart and hour >= 16 and hour < 18:
			gs 'zz_render','','','Вы видите, что в парке прогуливается Рекс'
			act 'Подойти к Рексу':gt 'reks','skver'
		end
		! бойфрейнд гуляет в парке
		if GvstreBoy = 1: gs 'zz_render','','','Вы видите, что в парке прогуливается <a href="exec:numnpc = SkverBoy & GT ''Gnpc''"><<$nameBoyfrend[SkverBoy]>></a>.'
		! гопота
		if sunWeather >= 0 and temper > 0 and ((hour >= 18 and hour < 20 and week < 6) or (week >= 6 and hour >= 10 and hour < 20)):
			gs 'zz_render','','','Вы видите как в парке на лавочке сидят <a href="exec: gt''gopskver''">Витёк Котов, Дэн Рыжов и Шульга.</a>'
		end
		! мать гуляет в парке
		gs 'zz_family','mother_sheduler'
		! meynold`s events
		if $npc['15,qw_day'] ! day: gs 'mey_vika_events', 'park_meet'
	else
		! проверка машини возле парка
		gs 'car', 'check'
		! кафешка в парке
		if hour >= 10 and hour <= 20:
			act 'Посидеть в кафе в парке':
				*clr & cla
				minut += 30
				gt 'parkKafe','food'
			end
		end
		! наркоманы
		if hour > func('zz_weather','sunrise') and hour < func('zz_weather','sunset') and pusher_day ! day:
			pusher_day = day
			if narkossister <= 3 and SNarkPriton = 0: gs 'zz_render','','','Возле одной из лавок валяются <a href="exec:gt''zz_park'',''nark''">торчки</a>.'
			if pusher > 0:
				gs 'zz_render','','','Вы видите торговца дурью, трущегося у входа в парк.'
				act 'Купить бледную леди (2000 руб 1 доза)':
					cla
					minut += 10
					i = input ("Сколько доз вы хотите купить?")
					if i*2000 >= money or i <= 0:
						gs 'zz_render','','','<font color=red>У вас недостаточно денег для покупки!</font>'
					else
						money -= i*2000
						dur += i
						gs 'zz_render','','common/park/drug_dealer.jpg','Вы быстро рассчитались и спрятали пакетик, переданный вам торговцем, в карман. Потом вы сможете нюхнуть у себя дома в спальне.'
					end
					act 'Уйти': gt 'zz_park'
				end
				exit
			end
		end
		! если знакомы с Таней - можно c ней поболтать
		if hour >= 18 and hour <= 20 and week < 6 and $npc['31,qwTanyaMain'] >= 10:
			gs 'zz_render','','','В парке бегает <a href="exec:gt''tanya_events'',''talk''">Таня.</a>'
		end
		! бойфренд
		if bfa >= 1 and hour >= 15 and rand(0,10) <= 1: gt $curloc,'boyfriend'
		! дополнительные переходы в парке сити
		act 'Идти к панели возле парка': gt $curloc,'whore'
		act 'Идти к аттракционам': gt 'zz_park','lunapark'
	end
end
! стандартные действия в парке
if $args[0] = 'actions':
	! раздача флаеров
	if hour >= 10 and hour <= 15:
		act 'Подрабатывать раздачей флаеров (100 руб в час)':
			*clr & cla
			minut += 60
			money += 100
			manna -= 2
			gs 'stat'
			gs 'time'
			gs 'zz_render','',func('zz_funcs','mk_image','common/park/flaer'),'Вы целый час раздавали флаеры посетителям парка. После этого вы получили 100 рублей.'
			act 'Закончить': gt $curloc
		end
	end
	! бег в парке
	if func('zz_clothing','get_group') = 3 and $npc['0,park_cross'] ! day and sick < 1:
		act 'Бегать по парку (1 час)':
			*clr & cla
			minut += 60
			body['day_weight'] -= 1
			beg += RAND(3,6)
			gs 'zz_funcs','sport',2
			$npc['0,park_cross'] = day
			gs 'stat'
			gs 'time'
			gs 'zz_render','',func('zz_funcs','mk_image','common/park/cross'),'Вы в течение часа бегаете по парку. Такие пробежки хорошо поддерживают форму и не дают заплыть жирком.'
			! входная точка ветки с Таней
			if hour >= 18 and hour <= 20 and week < 6 and $npc['31,qwTanyaMain'] < 10 and tanya['park_meet_day'] ! day and dom >= 50 and $control_point ! 'gorodok': gt 'tanya_events','park_meet'
			! --
			! маньячило
			if hour >= func('zz_weather','sunset'):
				if RAND(0,10) >= 8:
					gs 'zz_render','','','Из тени дерева вышел парень, он подошел к вам и процедив сквозь зубы "Шалава!", потащил вас в кусты.'
					act 'Драться': gt 'zz_fight',2
					exit
				end
			end
			! --
			act 'Далее': gt $curloc
		end
	end
	! прогулка в парке
	act 'Гулять по парку (30 мин)':
		*clr & cla
		minut += 30
		manna += 10
		gs 'stat'
		if pusher = 0 and $control_point ! 'gorodok':
			*clr & cla
			gs 'zz_render','Странная личность','common/park/drug_dealer.jpg','Молодой парень, одетый в кофту с капюшоном, подходит к вам и заговорщицким голосом говорит:**\\\- Девушка, у меня тут доза бледной леди есть, хочешь попробовать? Совершенно бесплатно. Прекрасная вещь, моментально прочищает мозги, настроение такое, что просто атас, ощущения в теле такие, как будто вы здоровее никогда не были.///'
			act 'Да':
				cla
				pusher = 1
				manna = 100
				nark += 10
				narkday = day
				gs 'stat'
				gs 'zz_render','','','Вы остановились с интересом, никогда не пробовали дурь, но ведь с одного раза ничего не будет? Парень подошел к вам и отсыпал немного белого порошка, вы затянули его носом и мир расцвел неожиданными красками.<br>Парень посмотрел на вас и сказал:**\\\- Если понравилось, приходи еще, доза всего 2000 рублей.///'
				act 'Уйти': gt 'zz_park'
			end
			act 'Нет': gt 'zz_park'
			exit
		end
		! миники
		if rand(0,10) < 2:
			gt $curloc,'mini'
		else
			if $npc['25,qwSonya'] >= 5 and $npc['25,qwSonya'] < 50 and hour >= 15 and hour < 20 and $control_point = 'gorodok':
				! Соня отбивает ГГ от гопоты
				if $npc['25,qwSonya'] = 15: gt 'sonya','help_in_park'
				! Соня сосет в парке
				if rand(0,10) = 1: gt 'sonya','suck_in_park'
			end
			gs 'zz_render','',func('zz_funcs','mk_image','common/park/walk'),'Вы не торопясь прогуливаетесь по парку, дыша свежим воздухом.'
			act 'Завершить прогулку': gt $curloc
		end
	end
	! посиделки на лавочке
	if temper >= 5:
		act 'Присесть на лавку':
			*clr & cla
			if func('zz_clothing','get_group') = 2:
				_park_img = 0
			elseif func('zz_clothing','is_schooldress') = 1:
				_park_img = 1
			elseif func('zz_clothing','get_group') = 4:
				_park_img = 2
			else
				_park_img = 3
			end
			gs 'zz_render','','common/park/bench/'+_park_img,'Ну и какого хрена она здесь делает?'
			gs 'zz_funcs', 'waiting'
			if reading['in_bag'] >= 0: act 'Читать '+func('zz_books','get_name',reading['in_bag']): gt 'zz_books','read_on_walk'
			! если ГГ бухая - может уснуть на лавке
			if alko >= 3 and rand(0,10-alko) <= 1:
				*clr & cla
				health += 5
				hapri = 0
				i = rand(1,alko/3)
				minut += i*60
				son += i*3
				if mop > 1: mop = 0 & vidageday -= 1
				gs 'stat'
				gs 'zz_render','','common/park/bench/sleep/'+_park_img,'Вы казалось только присели чуть отдохнуть на лавочке - как на вас накатила такая усталость и сморил сон.'
				act 'Потянуться':
					*clr & cla
					if func('zz_clothing','is_skirt') = 1:
						if tanga = 1:
							tanga = 0
							gs 'zz_render','','common/park/bench/sleep/4','Вы сладко потянулись, разведя руки и ноги, и почувствовали, что трусики куда-то делись! Испуганно сжав ножки, вы подумали: «надеюсь, кто забрал трусики - только ими и ограничился, а не воспользовался вашим состоянием и не сделал чего-то большего».'
						else
							gs 'zz_render','','common/park/bench/sleep/5','Вы сладко потянулись, разведя руки и ноги, сверкая своей голой писькой на весь парк.'
						end
					else
						gs 'zz_render', '', 'common/park/bench/sleep/6','Вы сладко потянулись.'
					end
					act 'Встать': gt $curloc
				end
				killvar '_park_img'
				exit
			end
			killvar '_park_img'
			exit
		end
	end
end
! выход с парка
if $args[0] = 'finish':
	if $control_point = 'gorodok':
		act 'Выйти из парка':
			minut += rand(5,10)
			gt 'gorodok'
		end
	else
		act 'В Северный район':
			minut += rand(20,40)
			gt 'nord'
		end
		act 'В Южный район':
			minut += rand(20,40)
			gt 'street'
		end
		act 'В Центр':
			minut += rand(20,40)
			gt 'down'
		end
	end
end
! наркоманы
if $args[0] = 'nark':
	*clr & cla
	gs 'time'
	SNarkPriton = 1
	gs 'zz_render','Упоротые','common/park/druggy.jpg','Торчки посмотрели на вас тупым отрешенным взглядом. Но один из них сказал:**\\\- Слыш, если дурь нужна, иди на хату, тут не продаем.///'
	act 'Отойти': gt 'zz_park'
end
! аттракционы
if $args[0] = 'lunapark':
	*clr & cla
	minut += 5
	gs 'stat'
	gs 'time'
	gs 'zz_render','Аттракционы','common/park/lunapark/lunapark.jpg','Аттракционы занимают достаточно большую часть парка и днём тут всегда суета из отдыхающих, влюбленных парочек и мамаш с детьми.'
	if vladimirQW = 0 and hour >= 12 and hour <= 18 and week > 5 and vnesh >= 40: gs 'zz_render','','','<a href="exec:GT ''qwloc''">Около ограды стоит крепкий мужчина и разглядывает вас</a>.'
	if hour >= 9 and hour <= 20:
		if money >= 75:
			act 'Съесть шаурму 75 руб':
				*clr & cla
				minut += 30
				health += 10
				money -= 75
				manna += 5
				cumlip = 0
				gs 'stat'
				gs 'zz_kitchen_acts','eat','images/common/park/lunapark/shaurma.jpg'
				act 'Отойти': gt 'zz_park','lunapark'
			end
		end
		if money >= 50:
			act 'Попить лимонада 50 руб':
				*clr & cla
				minut += 30
				money -= 50
				manna += 5
				cumlip = 0
				gs 'stat'
				gs 'zz_kitchen_acts','drink','','images/common/park/lunapark/lemonade.jpg'
				act 'Отойти': gt 'zz_park','lunapark'
			end
			act 'Ударить по силомеру (50 руб)':
				*clr & cla
				minut += rand(5,10)
				money -= 50
				gs 'stat'
				gs 'zz_render','','common/park/lunapark/silomer.jpg','Вы, что есть силы ударили по силомеру и на экране зажглась цифра ' + rand(stren*4/5,stren*6/5)
				act 'Отойти': gt 'zz_park','lunapark'
			end
		end
		if money >= 100:
			act 'Кататься на карусели 100 руб':
				*clr & cla
				minut += 30
				money -= 100
				manna += 10
				gs 'stat'
				gs 'zz_render', '', 'common/park/lunapark/karusel.jpg', 'Вы полчаса катались на карусели, и от этого увеличилось настроение'
				act 'Отойти': gt 'zz_park','lunapark'
			end
			act 'Стрелять в тире 100 руб':
				*clr & cla
				minut += 30
				money -= 100
				gs 'stat'
				gs 'zz_render', '', 'common/park/lunapark/tir.jpg', 'Вы решили попробовать себя в роли стрелка в тире. Перед вами несколько мишеней и всего 10 пулек.'
				act 'Стрелять':
					cla
					i = rand(agil - agil/5,agil + agil/5)
					if i >= 80:
						mishka += 1
						manna += 10
						gs 'zz_render','','','Вы точно положили все 10 пуль в цели и выиграли плюшевого мишку.'
					else
						manna += 5
						gs 'zz_render','','','<<i/8>> из 10 пуль попали в цель.'
					end
					gs 'stat'
					act 'Отойти': gt 'zz_park','lunapark'
				end
			end
		end
	end
	act 'Уйти': gt 'zz_park'
end
! миники
if $args[0] = 'mini':
	*clr & cla
	! создаем массивы взаимосвязанных строк и рисунков
	! летние
	$_str[0] = 'Проходя мимо вы замечаете парочку, играющую на лавочке в шахматы.'
	$_str[1] = 'Гуляя, вы замечаете сидящую на газоне девушку со щенком, преданно глядящим на неё.'
	$_str[2] = 'Идя по парку, вы замечаете идущую странной вихляющей походкой девушку. Она сворачивает на боковую аллею, и вы решаете проследить. Углубившись в парк, девушка неожиданно сгибает колени, задирает платье… и поправляет торчащий в попе здоровенный дилдо. Вот это да!'
	$_str[3] = 'В парке прогуливается молодая мама с коляской. Присмотревшись, вы замечаете, что трусики для неё - это явно не обязательный предмет гардероба.'
	$_str[4] = 'В парке по тротуару прогуливается молодая мама с коляской.'
	$_str[5] = 'В парке прогуливются две милые девушки.'
	$_str[6] = 'В парке две милые девушки явно решили привлечь внимание парней, демонстрируя свои аппетитные попки.'
	$_str[7] = 'Забравшись вглубь сквера, вы остановились неподалёку от девушки, устроившейся на скамейке. Она с кем-то говорила, пользуясь планшетом. Вы уже собирались уйти, как вдруг она вскинула голову и воровато осмотрелась, причем вас, похоже, не заметила. Затем спустив планшет между ног, задрала юбку, и вам и человеку по ту сторону планшета стало отлично видно, что трусиков на ней нет. Из планшета раздались, какие то восторженные крики, а вы хоть и отметили что у девушки пися довольно симпатичная, все же вас больше интересовало, неужели в парке появился Wi-Fi.'
	$_str[8] = 'Наслаждаясь свежим воздухом и природой, вы постепенно ушли дальше от нахоженных тропинок. Остановившись передохнуть под деревом, вы услышали, какой-то шум над головой: «Белки?» - мелькнула у вас мысль. Подняв голову, вы увидели голую девушку, которая упершись в ветки, которая задорно мастурбировала. «Ниче се белки» - усмехнулись вы, и помахав ей рукой пошли дальше.'
	$_str[9] = 'Забравшись подальше, вы неожиданно вышли на целую компанию, отдыхавшей в глубине парка. Они, веселясь, играли в твистер. Подойдя поближе, вы с улыбкой смотрели на это переплетение ржущих тел. «Ого, а эта девушка трусики не очень любит», - подумали вы глядя на бритую письку выглядывающую из под юбки, у одной из участниц. «Тут, похоже, весело игра закончиться», - мелькнула у вас еще одна мысль. И вы, все так же глупо улыбаясь, пошли дальше.'
	$_str[10] = 'Отойдя в сторону от суетных основных тропинок, вы наткнулись на девушку, перекинувшуюся через бревно, на вас отчетливо смотрели обе ее дырочки, ибо трусиков на ней не было, а платье было довольно короткое чтобы, что-то скрыть.  Вы кинулись к ней, испугавшись, что ей плохо, но она неожиданно разогнулась, и весело глянув на вас спросила:**\\\- Видела ящерицу? Такая красивая зеленая.///**У вас перед глазами все еще стояли две ее такие соблазнительные дырочки, а она про ящериц.'
	$_str[11] = 'В парке тихо и пустынно только одинокая собака принюхивается к чему-то возле тротуара.'
	$_str[12] = 'В парке прогуливается влюбленная парочка.'
	$_str[13] = 'В парке парень катает девушку на руле велосипеда, отчего та весело визжит.'
	$_str[14] = '"Похоже, сегодня кто-то останется без сладкого" - подумали вы, увидев парочку.'
	$_str[15] = 'В парке под деревом целуется влюбленная парочка.'
	! зимние
	$_str[16] = 'В парке прогуливается семейство, катая детей на санках.'
	$_str[17] = 'Вы забрались в глубину сквера, и людей вокруг почти не осталось. Вдруг в вас прилетает снежок: обернувшись, вы, видите двух обнажённых девушек, играющих в снежки. "Моржихи какие то", - зябко думается вам, хотя увиденное было довольно волнующе.'
	$_str[18] = 'В парке две милые девушки балуются сосульками.'
	$_str[19] = 'Несмотря на холодную погоду, вы забрались вглубь парка. Уже собираясь идти к выходу, вы вдруг замечаете что-то странное. Развернувшись, вы видите девушку в куртке, но без "низа": на ней нет ни юбки, ни штанов. И трусов тоже нет. Девушка эротично огинается вокруг кем-то слепленного снеговика, а вокруг бегает парень, беспрестанно щёлкая фотоаппаратом.'+iif(voyeurism < 20,'**<dh>- Блин, не могли места поукромнее и потеплее найти! -</dh> фыркаете вы, ёжась от мороза.<dh> - Застудит, дура, письку - детей не будет!</dh>',iif(voyeurism < 40,'**<dh>- Но холодно же! Так и письку застудить недолго, -</dh> думается вам.',iif(voyeurism < 60,'**<dh>- Хм, а это было б интересно! -</dh> ухмыляетесь вы.<dh> - Я б тоже так попробовала, если б не так холодно б было!</dh>','**<dh>- А я б тоже так не прочь попробовать! Но холодно...</dh>')))
	$_str[20] = 'Пройдя вглубь сквера, вы замечаете грустную девушку, которая слепила необычного снеговика. Ну, во всяком случае, она ему член приделала, из снега его вылепив. И, к вашему удивлению, принялась его сосать словно настояший. Вы, было подивились необычному способу наесться снега, но тут замечаете какого-то парня, мечущегося вокруг девушки, постоянно щёлкая фотоаппаратом. "Вот дура-то! - думается вам. - Жопу отморозит - детей не будет!" - хмыкнув, вы топаете к выходу из парка.'
	$_str[21] = 'Несмотря на холодную погоду, вы забрались вглубь парка, и заметили странную парочку, которая, несмотря на холодную погоду, трахается прямо посреди дорожки, и еще при этом прикрываясь зонтом, причем не от любопытных глаз, а от неба. Нахера им это зонт, зимой-то? - ржёте вы про себя. - Укурки какие-то. Фыркнув, вы проходите мимо, сделав вид, что вас пофиг.'
	$_str[22] = 'Любуясь заснеженными деревьями, вы и не заметили, как оказались довольно далеко от входа в парк, в его "дикой части". Дорожки тут пустынные. Однако навстречу вам вдруг выскакивает девушка в шубе, почему-то улыбалась. "Классная шубка! - непроизвольно оцениваете вы наряд. Тут девушка как-то неловко и ненатурально подскальзывается и хлопается на попу. Полы шубы распахиваются: под шубой у девушки нет ничего, кроме самой девушки. Совершенно голой. На ней даже белья нет. Заметив, куда вы смотрите, девушка, рассмеявшись, и подмигнув вам, вскакивает, и, запахнувшись, рысцой бежит дальше. - "Вот дура-то! - думается зябко вам. - Ёбаря б лучше нашла, полоумная"'
	$_str[23] = 'В парке веселятся две милые девушки.'
	$_str[24] = 'В зимнем парке вам встретилась влюбленная парочка.'
	$_str[25] = 'Девушка с парнем играют снежки, голосно смеясь на весь парк.'
	! получаем рандомный номер выводимого ивента
	i = iif(month >= 3 and month <= 10,rand(0,15),rand(16,25))
	! вывод
	gs 'zz_render','','common/park/mini/'+i,$_str[i]
	! очистка переменных
	killvar '$_str'
	! кнопка выхода
	act 'Уйти': gt $curloc
end
if $args[0] = 'boyfriend':
	*clr & cla
	gs 'zz_render','Фото идущего по парку бойфренда','','В парке вы замечаете как вам на встречу идет ваш парень <<$boyA>>'
	act 'Уйти': bfa -= 1 & gt $curloc
	if boyonceA = 0 or (boyonceA = 1 and dom >= 0):
		gs 'zz_render','','','<<$boyA>> обрадовался вам и взяв вас за руку повлек за собой глубже в парк.'
		if boyonceA = 1: act 'Предложить посидеть на скамейке': gt $curloc,'boyfriend_sex'
		act 'Мило побеседовать':
			cla
			bfA += rand(2,5)
			minut += rand(15,30)
			gs 'zz_render','','','<<$boyA>> обрадовался вашей случайной встрече и обнял вас. Вы мило побеседовали и поцеловавшись на прощание разошлись каждый по своим делам.'
			act 'Завершить прогулку': gt $curloc
		end
	else
		gs 'zz_render','','','<<$boyA>> обрадовался вам и взяв вас за руку повлек за собой глубже в парк.'
		act 'Идти с парнем':  gt $curloc,'boyfriend_sex'
	end
end
if $args[0] = 'boyfriend_sex':
	*clr & cla
	gs 'zz_render','','common/park/sex/obnimashki.jpg','Вы находите тихий, заброшенный тупик и прижавшись к стене начинаете целоваться, но вам хочется большего.'
	act 'Подрочить':
		*clr & cla
		hj += 1
		DOM += 1
		bfA += 1
		gs 'zz_render','','common/park/boy0','Не разрывая поцелуя вы ловко расстегиваете штаны <<$boyA>> и начинаете надрачивать его дружка. Не выдержав такого напора он кончает прямо на землю.'
		act 'Завершить прогулку': gt $curloc
	end
	act 'Отсосать':
		*clr & cla
		bfA += 2
		bj += 1
		gs 'zz_render', '', 'common/park/boy1','Вы встаете на колени, расстегиваете ширинку партнера, и резко погружаете член себе в ротик, смотря вверх на его расслабленное лицо. После нескольких минут лицо <<$boyA>> напрягается в преддверии оргазма.'
		act 'Продолжать сосать':
			cla
			swallow += 1
			gs 'zz_funcs', 'cum', 'lip'
			gs 'zz_render','','','Струя горячей спермы ударила вам в рот. Вы начали глотать сперму, а <<$boyA>> продолжал кончать вам в рот. Наконец поток горячей жидкости с резким вкусом прекратился.'
			act 'Уйти': gt $curloc
		end
		act 'Вынуть изо рта':
			cla
			if harakBoyA = 0:
				gs 'zz_render','','','Вы вынули член изо рта и <<$boyA>> кончил вам в ладонь.'
			elseif harakBoyA = 1:
				if rand(0,3) < 3:
					gs 'zz_render','','','Вы вынули член изо рта и <<$boyA>> кончил вам в ладонь.'
				else
					gs 'zz_funcs', 'cum', 'face'
					gs 'zz_render','','','Вы вынули член и в этот момент <<$boyA>> разрядился прямо вам на лицо, покрывая его теплой и липкой спермой.'
				end
			else
				gs 'zz_funcs', 'cum', 'face'
				gs 'zz_render', '', '','<<$boyA>> удерживая вас за волосы вынул член из вашего рта и кончил вам на лицо, покрывая его теплой и липкой спермой.'
			end
			act 'Уйти': gt $curloc
		end
	end
	act 'Секс':
		*clr & cla
		sex += 1
		bfA += 3
		pose = 1
		$boy = $boyA
		dick = dickA
		silavag = silavagA
		gs 'zz_render','','images/picV/dog.jpg','Вы находите чистый пяточек и стягиваете штаны, призывно качая попкой. <<$boyA>> толкает вас, ставя раком, и резко входит в вас.'
		gs 'zz_dynamic_sex', 'sex_start'
		gs 'zz_dynamic_sex', 'vaginal', 'dick'
		gs 'zz_dynamic_sex', 'sex_cum'
		act 'Завершить прогулку': $curloc
	end
end
! вор
if $args[0] = 'theft':
	*clr & cla
	$npc['0,theft_month'] = month
	gs 'zz_render','Вор','common/park/thieft.jpg','Вы подверглись ограблению, у вас из рук воришка вырвал сумочку с деньгами и быстро рванул прочь.'
	act 'Не побегу, мне его не догнать':
		money = 0
		gt $loc,$metka
	end
	act 'Догоню этого урода и тогда ему пиздец':
		if speed < 60 and vital < 60:
			cla
			minut += rand(2,5)
			money = 0
			gs 'stat'
			act 'Не догнала, надо тренироваться': gt $loc,$metka
		else
			RapistFight = 1
			gt 'zz_fight', 1
		end
	end
end
if $args[0] = 'robber':
	*clr & cla
	$npc['0,theft_month'] = month
	gs 'zz_render', 'Вымогатель','common/park/robber.jpg','Неожиданно на вас напал грабитель'
	act 'Отдам деньги, здоровье важнее':
		*clr & cla
		money = 0
		gs 'stat'
		gs 'zz_render','','','Вы потеряли все имеющиеся у вас в наличии сбережения.'
		act 'Уйти': gt $loc,$metka
	end
	act 'Хрен ему, а не деньги':
		cla
		gs 'zz_render','','','Грабитель с грязными ругательствами набрасывается на вас, теперь просто деньгами вы не отделаетесь.'
		act 'Драться': gt 'zz_fight',1
	end
end
if $args[0] = 'rapist':
	*clr & cla
	gs 'zz_render','Маньяк с ножом','common/park/sex/maniak','Внезапно на вас напал какой-то негодяй, он подошел к вам и процедив сквозь зубы:**\\\- Шалава, лучше не дёргайся!/// - и потащил вас в парковые кусты.'
	act 'Сопротивляться бесполезно':
		*clr & cla
		$boy = 'насильник'
		dick = rand(12,20)
		silavag = 0
		guy += 1
		rape += 1
		dom -= 1
		pose = 1
		gs 'zz_render','','common/park/sex/rape', 'Парень отводит вас в кусты и ставит вас раком к дереву, вы чувствуете как его твердый член входит в вашу вагину.'
		gs 'zz_dynamic_sex', 'vaginal', 'dick'
		gs 'zz_dynamic_sex', 'sex_cum'
		gs 'zz_render','','','Парень вытаскивает  из вас член, натягивает  штаны и уходит.'
		act 'Уйти': gt $loc,$metka
	end
	act 'Драться': gt 'zz_fight',2
	exit
end
if $args[0] = 'whore':
	*clr & cla
	minut += 5
	gs 'time'
	gs 'stat'
	if hour >= 16 and hour <= 23:
		gs 'zz_render','','common/park/prostitute.jpg','По обочине дороги гуляют <a href="exec:GT ''prostitute'',''start''">проститутки</a>, иногда к ним подъезжают машины и забирают их.'
	else
		gs 'zz_render','',func('zz_funcs','mk_image_ex','common/park/park'),'Автомобили проезжают по дороге мимо парка и тут не видно ничего интересного.'
	end
	act 'Идти в парк': gt $curloc
end