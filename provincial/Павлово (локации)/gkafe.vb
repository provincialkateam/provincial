$metka = $ARGS[0]
$loc = $CURLOC
gs 'zz_phone','boyfriend_call_init'
act 'Выйти': gt 'vokzalG'
gs 'stat'
gs 'zz_funcs', 'colorize_day'
gs 'zz_render','Кафешка','qwest/alter/gkafe'+iif(hour >= 8 and hour <= 20,'2','')+'.jpg','Маленькое задрипанное, привокзальное кафе.'
if hour >= 8 and hour <= 20:
	if $mother['location'] = 'gkafe' and hour = 16:
		gs 'zz_render','','','Дверь закрыта на замок и на двери висит табличка "Учет".'
		act 'Гулять вокруг кафе':
			*clr & cla
			minut += 15
			gs 'stat'
			gs 'zz_render','','','Вы прошлись вокруг кафе и вдруг до вас дошли слабые голоса, приблизившись к стене кафе, вы услышали какие то слабые женские стоны. Зайдя за кафе вы видите старые ящики и на стене маленькое окошко в которое можно заглянуть, если встать на ящики.'
			act 'Уйти': gt'vokzalG'
			act 'Подглядеть в окно':
				*clr & cla
				minut += 5
				gs 'stat'
				gs 'zz_render','','qwest/alter/placer/momslut.jpg','Вы залезли на ящики и заглянули в маленькое окошко'
				if momslut > 0:
					gs 'zz_render','','','За стеклом вы заметили, что ваша мама делает минет хозяину кафе. Вы стали рассматривать, как мама с большим энтузиазмом сосет член заглатывая его до самых яиц.'
				else
					momslut = 1
					gs 'zz_render','','','За стеклом вы заметили, как какая то женщина сидит на корточках перед мужчиной и что-то делает. И тут до вас дошло, что это ваша мама делает минет хозяину кафе. Вы от удивления выпучили глаза, а мама тем временем сосала член с большим энтузиазмом заглатывая его до самых яиц.'
				end
				act 'Уйти': gt 'vokzalG'
				act 'Смотреть дальше':
					cla
					*clr
					minut += 5
					gs 'stat'
					gs 'zz_render','','qwest/alter/placer/momslut2.jpg','Ваша мама встала на ноги и повернулась своим большим голым задом к хозяину кафе. Он подошел сзади и резко вогнал свой член ей в киску. Его движения были сильны и размашисты, ваша мать стонала и держалась за стенку. Наконец хозяин кафе схватив зад вашей матери, застонал и кончил ей в киску.'
					act 'Уйти': gt'vokzalG'
				end
			end
		end
	else
		gs 'zz_render','','','Так как тут работает ваша мама, то вас кормят бесплатно.'
		if week < 6: gs 'zz_render','','',iif(hour >= 8 and hour < 17,'В кафе за стойкой стоит ваша <a href="exec:GT ''mother''">мама</a>.','За барной стойкой стоит хозяин кафе Слава')
		act 'Поесть':
			*clr & cla
			gs 'zz_kitchen_acts', 'eat'
			gs 'zz_kitchen_acts', 'drink', 'tea'
			if rand(1,10) > 7 and hour > 16 and hour <= 20:
				if age < 18 and $npc['42,qwDyadyaMisha'] = 0 and momslut >= 1:
					gs 'zz_render','','','Мимо вашего столика идет к выходу взрослый мужчина, в котором вы узнаете дядю Мишу, вашего соседа. Он увидел вас, остановился и повернувшись к вам, сказал:**\\\- О, <<$name[1]>>, здравствуй. Узнала меня? Вижу, что да. Послушай, тут дело такое... Мы с твоим отчимом как-то чинили машину допоздна, а потом завалились ко мне опрокинусь по рюмашке, и у меня на квартире осталась его сумка с инструментами. Вернёшь отцу сумку? Я тебе на мороженое денежку дам.///'
					act 'Согласиться': gt 'misha','0'
					act 'Отказаться': gt 'gkafe'
				elseif $npc['42,qwDyadyaMisha'] = 1 and dmishaday ! day:
					gs 'zz_render','','','Вдруг к вам за столик подсаживается дядя Миша, который раздевает вас взглядом, глядя алчными и маслянистыми глазами на вашу грудь, и говорит:**\\\- Привет, <<$name[2]>>.. может заглянешь сегодня ко мне на чай..? Повеселимся как в прошлый раз, да и денежкой не обижу, а?///'
					act 'Согласиться': gt 'misha','1'
					if dom => 0 and horny < 70 and misha_refuse <= 3:
						act 'Отказаться':
							*clr & cla
							misha_refuse += 1
							gs 'zz_render', '', 'pavlovo/misha/misha.jpg', '\\-Извини, дядя Миша, //- ответили вы. \\- Сейчас не могу - времени нет, как-нибудь в другой раз.//**\\\-Ловлю тебя на слове!, -/// улыбнулся дядя Миша.'
							act 'Уйти': gt 'gkafe'
						end
					end
				else
					act 'Встать из за стола': gt 'gkafe'
				end
			else
				act 'Встать из за стола': gt 'gkafe'
			end
		end
		if (sunWeather < 0 or temper <= 0) and hour >= 18 and week >= 6 and hour >= 12 and hour < 20:
			gs 'zz_render','','','За столиком в дальнем углу потягивают пиво Витек Котов, Дэн Рыжов и Шульга.'
		end
	end
else
	gs 'zz_render','','','Кафе не работает.'
end