$gvokzal_description = {
	gs 'zz_render','Вокзал','images/pic/city/vokzal'+iif(snow > 0,iif(hour >= func('zz_weather','sunrise') and hour <= func('zz_weather','sunset'),3,2),iif(hour >= func('zz_weather','sunrise') and hour <= func('zz_weather','sunset'),4,1))+'.jpg'
}
$metka = $ARGS[0]
$loc = $CURLOC
gs 'zz_phone','boyfriend_call_init'
*clr
gs 'stat'
gs 'time'
dynamic $gvokzal_description
gs 'zz_render','','','Маленькая тихая станция. Около <a href="exec:minut += 3 & GT ''vokzalGin''">вокзала</a> стоит <a href="exec:minut += 5 & GT ''gkafe''">кафешка</a>. Также возле вокзала находится небольшой отель для приезжих, почтовое отделение и отдел милиции.**
На привокзальной площади расположен маленький <a href="exec:minut += 1 & GT ''grinok'',''main''">рынок</a>**
Небольшой <a href="exec:minut += 1 & GT ''hotel''">отель</a> в котором живут в основном командировочные.'
if money >= 5000 and hour > 9 and hour <= 17 and RAND(0,3) = 0 and deception_comp = 0:
	*clr
	dynamic $gvokzal_description
	gs 'zz_render','','','По пути к вокзалу к вам подходит девушка с микрофоном и мужчина с телекамерой. Девушка обращается к вам:**\\\- Здравствуйте! Мы с телевидения. Сейчас проходит беспрецедентная акция - шоу по раздаче подарков! И мы выбрали именно вас!///**Девушка достаёт красиво обёрнутую коробку с бантом и подаёт вам. \\\- Здесь ноутбук последнего поколения!///'
	if intel > 70 or deception_comp = 1: act 'Уйти': gt $curloc
	act 'Взять коробку':
		*clr & cla
		dynamic $gvokzal_description
		gs 'zz_render','','','Вы берёте коробку, а оператор принимается снимать вас на камеру. Девушка подносит микрофон и предлагает передать приветы родным и близким. Вы стоя с увесистой коробкой перед камерой передаёте приветы и благодарите за подарок. Мужчина всё снял и девушка просит вас оплатить НДС со стоимости подарка, мол, так по закону положено.**\\\- Это всего каких то пять тысяч!///'
		act 'Оплатить НДС':
			*clr & cla
			money -= 5000
			deception_comp = 1
			gs 'stat'
			dynamic $gvokzal_description
			gs 'zz_render','','','Вы отдаёте деньги и принимаетесь распечатывать подарок. Когда вам удаётся вскрыть коробку, вы видите, что там вместо ноутбука лежит кирпич, обёрнутый газетой. Вы оглядываетесь в поисках девушки и мужчины, но их уже и след простыл.'
			act 'Уйти': minut += 15 & gt $curloc
		end
	end
	exit
elseif hour > 9 and hour <= 17 and RAND(0,3) = 0 and func('zz_clothing','is_glamour') > 0 and deception_clothes = 0:
	*clr
	dynamic $gvokzal_description
	gs 'zz_render','','','По дороге к вокзалу, вы замечаете двух мужчин, которые при виде вас начинают шептаться между собой, а потом направляются в вашу сторону, произнося на ходу:**\\\- Ну просто фотомодель, ну прямо Клаудия Шиффер какая-то...///**Один из них, в представительном костюме, подойдя к вам произносит:**\\\- Мы открываем в вашем городе новый магазин и нам нужна девушка для рекламы. Мы предлагаем вам 30 тысяч за фотографию на рекламу.///'
	if intel > 70: act 'Уйти': gt $curloc
	act 'Согласиться':
		*clr & cla
		gs 'zz_render','','','Вы охотно соглашаетесь, на такое заманчивое предложение. Вас отвели к стройплощадке рядом с вокзалом. Один из парней достал фотоаппарат, и поставив его напротив дерева с кустами, сказал:**\\\- Снимать будем на натуральном фоне. Так живописней. Только вот проблема, ваше платье бликует. Вам нужно переодеться.///** Он достал из багажника машины платье и подал вам:**\\\- Переодеться можете прямо в машине...///**Парень показал вам на микроавтобус, который стоял неподалеку.'
		act 'Переодеться':
			*clr & cla
			stolen_clothes = current_clothing
			gs 'zz_clothing', 'init_clothing'
			gs 'zz_clothing', 'add_to_wardrobe', 5
			gs 'zz_clothing', 'dispose'
			zz_id = func('zz_clothing','is_exists', 5)
			if zz_id >= 0: gs 'zz_clothing', 'redress', zz_id
			gs 'zz_clothing', 'remove_at', stolen_clothes
			killvar 'zz_id'
			gs 'stat'
			gs 'zz_render','','','Вы залезли в машину и переоделись в платье, которое выдал вам парень.'
			act 'Позировать':
				*clr & cla
				gs 'stat'
				gs 'zz_render','','','Вы вышли из машины, оставив свои вещи в ней, и подошли обратно к дереву. Один из парней встал за фотоаппарат, второй суетится возле вас, показывая вам, как надо стоять. Фотоаппарат щелкнул.**\\\- Так, а теперь фотография со спины...///**Вы встали спиной к фотоаппарату и услышали голос:**\\\- Отлично. Теперь чуть приподними голову и закрой глаза.///**Вы исполнили указания, но никакого щелчка фотоаппарата не последовало. Вместо этого вдруг заработал двигатель машины.'
				act 'Обернуться':
					*clr & cla
					money = 0
					deception_clothes = 1
					gs 'stat'
					gs 'zz_render','','','Вы обернулись и увидели, что никакого фотоаппарата нет. Оба мужчины сидят в машине, которая тронулась с места и рванула по улицам. Вы лихорадочно сообразили, что свое платье и деньги вы оставили в машине. Вас кинули как последнюю лохушку...'
					act 'Уйти':minut += 15 & gt $curloc
				end
			end
		end
	end
	exit
end
act 'Уйти в городок': minut += 10 & gt 'gorodok'
act 'Выйти на северное шоссе': minut += 5 & nroad = 40 & gt 'road'