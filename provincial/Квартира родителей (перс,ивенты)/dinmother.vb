if $args[0] = 'talk':
	cla
	gs 'npc_editor','get_npc_profile',37
	$_str[0] = 'Вы пытаетесь поговорить с мамой, но она отказывается с вами говорить.'
	$_str[1] = 'Вы пытаетесь поговорить с мамой, но она разговаривает с вами сухо.'
	$_str[2] = 'Вы болтаете с мамой, она рассказывает вам, '
	$_str[3] = 'как в юности они с друзьями прикалывались над учителями в школе.'
	$_str[4] = 'как правильно краситься и ухаживать за кожей.'
	$_str[5] = 'как она любит ездить в деревню, на природу.'
	$_str[6] = 'что на рынке можно найти много дешёвых и полезных вещей.'
	$_str[7] = 'что раньше работала продавщицей на рынке, но из-за маленького заработка и постоянных приставаний кавказцев пришлось уволиться.'
	$_str[8] = 'как скучает по своей молодости.'
	$_str[9] = 'как она была зла на Аню, когда та не поступила в институт.'
	$_str[10] = 'как важно получить образование.'
	$_str[11] = 'что ваш отчим очень предсказуем - работа, телевизор, гараж. Никакой изюминки, но зато надёжен.'
	$_str[12] = 'что постоянные пьянки отчима с дядей Мишей по вечерам в гараже ни к чему хорошему не приведут.'
	$_str[13] = 'что любит по выходным гулять в парке или загорать на пляже.'
	$_str[14] = 'что вопреки всеобщему мнению - её босс - очень чуткий и отзывчивый человек, и именно о таком она когда-то в юности мечтала.'
	$_str[15] = 'жалуясь, что отчим в последнее время сбегает в гараж, как только она приходит с работы.'
	$_str[16] = 'что за работой у неё совсем нет времени ходить по магазинам и рынкам, так что приходится тратить время по выходным.'
	$_str[17] = 'вспоминая, что как-то застала Кольку, когда тот подглядывал за Аней в душе - он тогда несколько дней на задницу сесть не мог.'
	$_str[18] = 'довольно улыбаясь, что как-то на пляже видела, как Аня целовалась со своим парнем - стройным, симпатичным, рыжоволосым. Совсем, мол, выросла, скоро замуж пойдет.'
	$_str[19] = 'жалуясь, что у Ани вошло в привычку каждую пятницу напиваться до чертиков и никакие профилактические беседы не помогают.'
	$_str[20] = 'жалуясь, что Кольке, кроме футбола, пора б уже и на девочек обратить внимание.'
	$_str[21] = 'жалуясь, что у Кольки с учебой большие проблемы и старшим сестрицам не мешало бы ему помогать.'
	$_str[22] = 'что по понедельникам отчим ездит на газели за товарами на склады в город, и обычно возвращается поздно ночью.'
	gs 'zz_render','','',$_str[iif($npc['37,relation']>=40,2,$npc['37,relation']/20)] + $_str[iif($npc['37,relation']>=40,rand(3,22),'')]
	if $npc['37,relation'] < 40:
		if mothersorryday ! day:
			act iif($npc['37,relation'] <= 20,'Просить прощения','Пытаться сгладить отношения'):
				cla
				mothersorryday = day
				gs 'npc_editor','change_rep','+', 37, 10
				gs 'zz_render','','',iif($npc['37,relation'] <= 20,'Вы извиняетесь перед мамой и она, кажется, вас прощает.','Вы подлизываетесь к маме и она, кажется, начинает к вам лучше относится.')
				act '<B>Отойти</B>': gs 'dinmother', 'go_out'
			end
		end
	end
	act '<B>Отойти</B>': gs 'dinmother', 'go_out'
	killvar '$_str'
end
if $args[0] = 'memories':
	cla
	gs 'npc_editor','get_npc_profile',37
	$_str[0] = '<do>- Я выросла в деревне, в Гадюкино нашем</do> - пускается мать в воспоминания. <do>- Здорово там в детстве было! Ни забот, ни хлопот! С речки летом не вылезала целыми днями, лес вдоль и поперёк излазила. И как только волки не сожрали?</do> - искренне недоумевает она.<br><do>- Когда я была в твоём возрасте, даже помоложе на пару годков, - увивались там за мной</do> - тут мама чуть запнулась <do>- один... парень.</do><br><dh>- Иии?</dh> - навострили уши вы.<br><do>- Да ничего не получилось</do> - вздыхает мама. <do>- Я после девятого класса поехала в Павлово, поступать на кулинара учиться. Познакомились с Мишкой. Так в Павлово и осталась. А Андрей служить ушёл, вот и разошлись дорожки.</do>'
	$_str[1] = '<dh>- Ма, а как вы с нашим с Анькой отцом познакомились?</dh><br><do>- Да ничего особенного, доча</do> - с лёгкой грустью улыбается мать. <do>- На дискотеке мы с ним познакомились. Он потанцевать пригласил. Ну а дальше цветочки-конфетки-посиделки при Луне... А как выяснилось, что у меня после таких "посиделок" ребёнок будет - Мишка замуж предложил. А я и согласилась. А через пару лет и ты появилась.</do>'
	$_str[2] = '<dh>- Мам, а почему вы с нашим с Анькой отцом разошлись?</dh><br>Мать призадумалась:<br><do>- Так вот сразу и не объяснишь... Любовь-морковь? Так, химия одна. Была ли та любовь или просто гормоны играли? Не знаю. Мишка как узнал, что я беременна, тут же замуж предложил за него выйти. Свадьбу отгуляли, начали жить. Квартирку-однушку родители нам спроворили - не пришлось по съёмным углам мыкаться, да ещё и с дитём малым на руках. И началось как в сказке после слов "И я там был, мёд-пиво пил". Сказка кончилась, начались будни. Мишка в автобазу устроился водилой: была тут у нас, да загнулась давно, - а я сперва дома с Анькой сидела. Ох, и крикливая ж она была - плакса страсть! Орать часами могла. Что такое "спать" я на несколько лет позабыла. Все соседи меня возненавидели из за её воплей постоянных. Мишка, чую, потому в дальнобойщики и перевёлся - чтобы хоть в рейсах от Анькиного рёва отдыхать. А мне-то от неё никуда не деться! С недосыпа лютого злющая стала как собака, сварливая, мужа пилила постоянно. За собой следить перестала: в такую лахудру превратилась, жопу отъела с автобус - аж вспоминать стыдно...<br>А Мишка, видать, в рейсах погуливать на сторону начал. Женщины такие вещи замечают. В общем, склочиться начали мы с ним что ни день, как он не в рейсе. Чуть не до драк доходило. Мирились, правда, тоже бурно! - хихикает мать вдруг. - Вот после одного из таких замирений ты и появилась!<br>Мишка тут не выдержал. Сперва начал у друзей всяких ночевать оставаться, а там и к бутылочке прикладываться. Скандалили мы страшно. А как-то уехал он в рейс - и домой не вернулся. Сообщил, что подаёт на развод, но дочек, вас, бишь, содержать будет. А там с Вовкой, отчимом твоим, познакомились. Он вас как родных принял. Дочами всегда считал своими, нянькался с вами. Он вам с Анькой куда больше папа, чем Миша, который вас мне заделал. Ваш отчим вас обеих вырастил. А о Мишке я даже и вспоминать не хочу.</do>'
	$_str[3] = '<do>- Ездили мы в свадебное путешествие с Вовкой в Петербург по турпутёвке - нам её на свадьбу подарили. Красотища! Каналы ихние, Эрмитаж, Медный всадник, этот, как его, Петергоф... Вот только, за всю поездку, за двенадцать дней, всего четыре ясных дня было. Мы, правда, не столько по экскурсиям ходили, сколько по Апрашке да Уделке бегали - рынки это такие в Питере. Но и по экскурсиям тоже.</do>'
	gs 'zz_render','','',$_str[rand(0,3)]
	act '<B>Отойти</B>': gs 'dinmother', 'go_out'
	killvar '$_str'
end
if $args[0] = 'clothes':
	if current_clothing < 2:
		cla
		act '<B>Отойти</B>': gt 'bedrPar'
		if motherKnowWhore = 0 and motherKnowSpravka = 0 and $npc['37,qwMother'] = 0:
			if momslut < 2:
				gs 'zz_render','','','Мать посмотрела на вас гневным взглядом:<br><do>- <<$name[1]>>, как тебе не стыдно ходить '+iif(current_clothing=1,'в таком виде','голой')+' по дому? А ну бегом оделась!</do>'
			else
				gs 'zz_render','','','Мать грустно покачала головой:<br><do>- Ты бы дома хоть '+iif(current_clothing=1,'задницу-то прикрыла','оделась')+', а то ещё и в семье все будут знать, что ты шлюха...</do>'
			end
		elseif (motherKnowWhore + motherKnowSpravka) > 0 and $npc['37,qwMother'] = 1:
			if momslut < 2:
				gs 'zz_render','','','Мать посмотрела на вас гневным взглядом:<br><do>- Даже дома своей '+iif(current_clothing>0,'голой жопой','мандой')+' светишь, шлюха малолетняя! Пошла и оделась быстро! И не попадайся мне на глаза в таком виде!</do>'
			else
				gs 'zz_render','','','Мать грустно покачала головой:<br><do>- Ты бы дома хоть '+iif(current_clothing=1,'задницу-то прикрыла','оделась')+', а то ещё и в семье все будут знать, что ты шлюха...</do>'
			end
		elseif $npc['37,qwMother'] >= 2:
			gs 'zz_render','','','Мать строго посмотрела на вас:<br><do>- <<$name[1]>>, я, конечно, всё понимаю, но не могла бы ты хотя бы дома одеваться' +iif(current_clothing=1,' не так, как ты привыкла у своих хахалей расхаживать? У тебя ползадницы из-под полотенца видно!','? У хахалей своих в таком виде будешь расхаживать!') + '</do>'
		end
		exit
	end
	if func('zz_clothing','is_glamour') > 0 and hour > 9:
		gs 'zz_render','','','Мама посмотрела на вас: <do>- Ты куда это так нарядилась?</do>'
	end
	if pregTalkFamily = 0 and pregTalkMom = 0:
		if current_clothing > 2 and pregnancy > 150: gs 'zz_render','','','Мать смотрит на ваш явно выступающий под одеждой живот: <do>- <<$name[2]>>, залетела всё-таки, горе ты моё. Смотри, не вздумай аборт делать!.<do>'
		if current_clothing <= 2 and pregnancy > 90: gs 'zz_render','','','Мать смотрит на ваш животик: <do>- <<$name[2]>>, залетела всё-таки, горе ты моё. Смотри, не вздумай аборт делать!.</do>'
		pregTalkFamily = 1
		pregTalkMom = 1
		act '<B>Отойти</B>': gt $loc, $metka
		exit
	end
end
if $args[0] = 'piercing':
	motherpirsday = day
	if pirsA >= 1 and pirsAmother = 0:
		!пирсинг языка
		if rand(1,100) >= 75:
			cla
			gs 'zz_render','','','Мать заглядывает вам в рот: <do>- <<$name[1]>>, а что у тебя там на языке блестит?</do>'
			act 'Ничего':
				cla
				gs 'zz_render','','','Вы отрицательно качаете головой: "Ничего..." - и тут же отворачиваетесь, чтобы мать не заметила пирсинг языка.'
				act '<B>Отойти</B>': gt $loc, $metka
			end
			act'Показать пирсинг языка':
				*clr & cla
				pirsAmother = 1
				gs 'zz_render','','images/picb/piercing/pirsA.jpg','Вы открываете рот и высовываете язык, показывая матери пирсинг языка.<br>Мать качает головой: <do>- <<$name[1]>>, пирсинг языка делают распутные женщины, обслуживающие мужчин ртом.</do>'
				act '<B>Отойти</B>': gt $loc, $metka
			end
		end
		exit
	end
	if pirsB = 1 and pirsBmother = 0:
		!пирсинг губ
		cla
		gs 'zz_render','','','Мать смотрит на ваши губы, в которых появился пирсинг: <do>- А это ещё зачем?</do>'
		act 'Мне понравилось':
			*clr & cla
			pirsBmother = 1
			gs 'zz_render','','images/picb/piercing/pirsB.jpg','Вы пожимаете плечами и говорите, что просто вам понравилось, вот вы и решили сделать себе пирсинг губ.'
			act '<B>Отойти</B>': gt $loc, $metka
		end
		exit
	end
	if pirsC = 1 and pirsCmother = 0:
		!сережки
		pirsCmother = 1
		gs 'zz_render','','','Мать смотрит на ваши уши, замечая сережки: <do>- Я гляжу - ты решила носить сережки? Очень мило, тебе идёт.</do>'
		exit
	end
	if pirsD = 1 and pirsDmother = 0:
		!кольцо в носу
		pirsDmother = 1
		gs 'zz_render','','','Мать смотрит немного шокированно на ваше кольцо в носу: <do>- Господи, <<$name[1]>>, а это то ещё зачем? Я понимаю, быкам кольцо в нос вставляют, чтобы их за это кольцо таскать, но тебе-то зачем?</do>'
		exit
	end
	if pirsE = 1 and pirsEmother = 0:
		!пирсинг брови
		pirsEmother = 1
		'Мать качает головой, смотря на ваш пирсинг брови: <do> - <<$name[1]>>, ну зачем тебе эта ерунда в брови?<do>, - после чего сокрушённо качает головой - <do>Эх молодёжь, молодёжь...</do>'
		exit
	end
end
if $args[0] = 'hair':
	!проверка цвета волос
	gs 'zz_render','','','Мать с интересом посмотрелa на ваши волосы: <do> - Ух-ты, перекрасилась! ' + func('barbershop','change_haircolor') + '</do>'
	hcolmotherremember = hcol
end
if $args[0] = 'virgin_check':
	if school['certificate'] = 0:
		if GspravkaT = 0 and GspravkaTimes >= 3 and motherKnowSpravka = 0:
			cla
			if hour >= 7 and hour <= 17:
				gs 'npc_editor','change_rep','-', 37
				gs 'zz_render','','','Мать нахмурилась: <do>"Сколько можно тянуть со справкой? Придётся тебя самой к гинекологу вести!"</do><br>Мать взяла вас за руку и повела в поликлинику, к гинекологу.'
				act 'Идти к гинекологу за справкой':
					*clr & cla
					minut += rand(20,40)
					gs 'zz_render','','pavlovo/family/mother/ginekolog.jpg','Вы легли на гинекологическое кресло и раздвинули ноги. Гинеколог стала осматривать вашу вагину.'
					Gspravka = 7
					GspravkaT = iif(vagina > 0,2,1)
					gs 'zz_render','','','Гинеколог написала в справке, что вы '+iif(vagina > 0,'не','')+' девственница.'
					act '<B>Выйти</B>': gt $curloc
				end
			else
				gs 'zz_render','','','Мать нахмурилась: <do>Сколько можно тянуть со справкой? Мне что, тебя самой к гинекологу вести?!</do>'
				act '<B>Отойти</B>': gt 'dinmother', 'go_out'
			end
			exit
		elseif GspravkaT = 0 and GspravkaTimes < 3 and motherKnowSpravka = 0:
			GspravkaTimes += 1
			gs 'zz_render','','','<do>Тебе нужно сходить в поликлинику. К гинекологу, за справкой для школы.</do>'
		elseif GspravkaT = 2 and motherKnowSpravka = 0:
			cla
			if motherKnowWhore = 0:
				$npc['37,relation'] = 0
				motherKnowSpravka = 1
				if momslut < 2:
					gs 'zz_render','','','Мать схватила вас за ухо: <do>- Ах ты блядища! Мы с отцом надрываемся, чтобы из вас путных людей сделать, чтобы вы достойных мужей себе нашли, а ты, шлюха, уже успела манду кому-то подвернуть! Убирайся с глаз моих!</do>'
				else
					gs 'zz_render','','','Мать, грустно улыбнувшись, покачала головой: <do>- Ну вот ты и стала взрослой. Смотри не залети, доча.</do>'
				end
			elseif motherKnowWhore > 0 and $npc['37,qwMother'] < 2:
				gs 'npc_editor','change_rep','-', 37, 10
				motherKnowSpravka = 1
				gs 'zz_render','','','Мать посмотрела в справку: <do>- Даже и не удивительно. Трахаешься с кем-то в подворотне, да еще и домой во всех хахальских "подарках" приходишь. Глаза б мои тебя не видели...</do>'
			elseif $npc['37,qwMother'] >= 2:
				motherKnowSpravka = 2
				gs 'zz_render','','','<do>- И чего ты мне её принесла? Можешь хоть ксерокопии сделать и носить их в школу. Сомневаюсь, что у тебя там что-то изменится</do>, - с ехидством заметила ваша мать.'
			end
			act '<B>Отойти</B>': gs 'dinmother', 'go_out'
			exit
		end
	end
end
if $args[0] = 'money_talk':
	act 'Просить денег':
		cla
		minut += 5
		gs 'stat'
		gs 'dinmother', 'description'
		gs 'zz_render','','','Вы попросили у мамы денег.'
		if func('zz_reputation','get') = 4:
			gs 'zz_render','','','Мать возмутилась: <do>- <<$name[1]>>, и не стыдно тебе у матери деньги просить? Ты больше меня зарабатываешь!</do>'
			act '<B>Отойти</B>': gs 'dinmother', 'go_out'
			exit
		end
		$zz_no_money[0] = 'Мать нахмурилась: <do>- Вот же ты липучка. Ничего не получишь!</do>'
		$zz_no_money[1] = 'Мать нахмурилась: <do>Нет доча. Я тебе уже давала деньги. На сегодня хватит.</do>'
		$zz_no_money[2] = 'Мать нахмурилась: <do>Дочура, я же тебе уже дала денег!</do>'
		zz_rel = ($npc['37,relation'] - 1) / 20 - 1
		zz_money = zz_rel * 100 - 50
		if zz_money < 0: zz_money = 0
		if school['certificate'] = 0:
			if motherPayDay = day:
				gs 'zz_render','','', $zz_no_money[zz_rel-1]
			else
				gs 'npc_editor','change_rep','-', 37
				motherPayDay = day
				if zz_rel > 0:
					if school['progress'] < 70:
						zz_money = 0
					elseif school['progress'] >= 90:
						zz_money += 100
					end
				end
				$zz_money_str[0] = 'Мать нахмурилась: <do>- Будешь лучше учиться - будут тебе деньги, а пока ты заслужила только ремня, а не денег!</do>'
				$zz_money_str[1] = 'Мать нахмурившись дала вам <<zz_money>> рублей: <do>- Будешь лучше учиться, буду давать больше. А пока, это всё, что ты заслужила!</do>'
				$zz_money_str[2] = 'Мать улыбнулась и дала вам <<zz_money>> рублей: <do>- Ладно, держи.</do>'
				if zz_rel > 0:
					if school['progress'] < 70:
						gs 'zz_render','','',$zz_money_str[0]
					elseif school['progress'] >= 70 and school['progress'] < 90:
						gs 'zz_render','','',$zz_money_str[1]
					else
						gs 'zz_render','','',$zz_money_str[2]
					end
					money += zz_money
				else
					if motherKnowWhore = 0 and motherKnowSpravka = 0:
						gs 'zz_render','','','Мать нахмурилась: <do>- Не заслужила ты денег!</do>'
					end
					if motherKnowWhore >= 1 or motherKnowSpravka >= 1:
						gs 'zz_render','','','<do>- Ты ещё денег просишь, шлюха? Иди отсюда, катись к своим ёбарям деньги клянчить!</do>'
					end
				end
			end
			act '<B>Отойти</B>': gs 'dinmother', 'go_out'
		else
			gs 'zz_render','','','<do>Ты уже достаточно взрослая, зарабатывай сама.</do>'
		end
		killvar '$zz_money_str'
		killvar '$zz_no_money'
		killvar 'zz_rel'
		killvar 'zz_money'
		act '<B>Отойти</B>': gs 'dinmother', 'go_out'
	end
end
if $args[0] = 'go_out':
	if $mother['location'] = 'kuhrPar': gt 'kuhrPar'
	if $mother['location'] = 'sitrPar': gt 'sitrPar'
	if $mother['location'] = 'shop': gt 'shop', 'start'
	if $mother['location'] = 'grinok': gt 'grinok', 'main'
	if $mother['location'] = 'glake': gt 'glake'
	gt $loc, $metka
end
if $args[0] = 'cloth_talk':
	act 'Просить новую одежду':
		*clr & cla
		minut += 5
		gs 'stat'
		gs 'npc_editor','get_npc_profile',37
		gs 'zz_render','','','Вы попросили купить новую одежду.'
		if func('zz_reputation','get') = 4:
			gs 'zz_render','','','Мать возмутилась: <do>- <<$name[2]>>, у тебя от клиентов отбоя нет - и ты ещё у меня одежду просишь? Это я у тебя просить должна! Покупай сама.</do>'
			act '<B>Отойти</B>': gs 'dinmother', 'go_out'
			exit
		end
		if school['certificate'] = 0:
			if $npc['37,relation'] >= 80:
				if motherClotheMonth ! month:
					motherClotheMonth = month
					minut += rand(30,50)
					gs 'stat'
					gs 'zz_render','','','Мама тяжело вздохнула: <do>- Ну вот опять... Ты же меня по миру пустишь! Ладно, пойдем, горе луковое.</do>'
					gs 'zz_render','','','Вы вместе с мамой начали выбирать одежду. С мамой ходить за покупками совершенно не интересное занятие: она никогда не даст вам выбрать самостоятельно, и обязательно купит одежду на свой вкус.'
					act 'Выбрать одежду':
						*clr & cla
						newdress_timer = 0
						:loop_mother_buy_clothing
						if newdress_timer < 20:
							newdress_id = rand(68,126)
							if func('zz_clothing', 'is_exists', newdress_id) < 0:
								gs 'zz_clothing', 'init_clothing'
								gs 'zz_clothing', 'add_to_wardrobe', newdress_id
								gs 'zz_clothing', 'dispose'
								gs 'zz_render','','images/clothing/dress/' + newdress_id
							else
								newdress_timer += 1
								jump 'loop_mother_buy_clothing'
							end
							gs 'zz_render','','','Мама купила вам одежду.'
						else
							gs 'zz_render','','','Мать просмотрела и перещупала всю одежду в магазине, но так и не смогла ничего выбрать - некоторые платья у вас уже есть, другие ей не понравились.'
						end
						killvar 'newdress_id'
						killvar 'newdress_timer'
						act '<B>Отойти</B>': gs 'dinmother', 'go_out'
					end
					exit
				else
					gs 'zz_render','','','Мама покачала головой: <do>- Дочура, у нас бюджет не резиновый, в этом месяце мы уже покупали тебе одежду. Теперь жди следующего месяца.</do>'
				end
			else
				gs 'zz_render','','','Мама покачала головой: <do>- Нет денег.</do>'
			end
		else
			gs 'zz_render','','','Мама покачала головой: <do>- Ты уже взрослая, пора бы зарабатывать самой.</do>'
		end
		act '<B>Отойти</B>': gs 'dinmother', 'go_out'
	end
end
if $args[0] = 'tampon_talk':
	if $npc['37,relation'] >= 40 and mesec > 0 and pregnancy = 0 and tampon = 0:
		act 'Попросить тампон':
			gs 'zz_render','','','Вы рассказываете матери о том, что у вас начались месячные и закончились тампоны. Она понимающе смотрит на вас, приносит несколько своих и рекомендует вам в следующий раз позаботиться об этом заранее.'
			tampon = 3
			minut += 3
			cla
			act '<B>Отойти</B>': gs 'dinmother', 'go_out'
			exit
		end
	end
end
if $args[0] = 'cum_talk':
	cla
	act '<B>Отойти</B>': gs 'dinmother', 'go_out'
	if motherKnowWhore = 0 and motherKnowSpravka = 0 and $npc['37,qwMother'] = 0:
		$npc['37,relation'] = 0
		motherKnowWhore = 1
		if momslut < 2:
			gs 'zz_render','','','Мать посмотрела на вас, выпучила глаза и покраснела. На вас заметна сперма.'
			gs 'zz_render','','','<do>- Ах ты, шлюха малолетняя, это что такое на тебе? Всем говорю что дочь приличная, а она ебётся где-то. Блядина! Пошла отсюда, и не попадайся мне на глаза!</do>'
		else
			gs 'zz_render','','','Мать грустно покачала головой: <do>- Ты бы сперму то вытерла, а то весь город будет знать, что ты шлюха.</do>'
		end
	elseif (motherKnowWhore > 0 or motherKnowSpravka > 0) and $npc['37,qwMother'] = 1:
		if whoremday ! day: gs 'npc_editor','change_rep','+', 37, 20
		if motherKnowWhore = 0: motherKnowWhore = 1
		if momslut < 2:
			gs 'zz_render','','','Мать посмотрела на вас гневным взглядом. На вас заметна сперма.'
			gs 'zz_render','','','<do>- Опять где-то трахалась, шлюха малолетняя? Мне уже перед знакомыми стыдно за тебя! Пошла отсюда, не попадайся мне на глаза!</do>'
		else
			gs 'zz_render','','','Мать грустно покачала головой: <do>- Ты бы "майонез"-то вытерла, а то весь город будет знать, что ты шлюха.</do>'
		end
	elseif $npc['37,qwMother'] >= 2:
		if whoremday ! day: gs 'npc_editor','change_rep','+', 37, 10
		if motherKnowWhore = 0: motherKnowWhore = 1
		gs 'zz_render','','','Мать строго посмотрела на вас. На вас заметна сперма.'
		gs 'zz_render','','','<do>- <<$name[1]>>, я конечно все понимаю, но не могла бы ты убирать художества своих хахалей? Быстро приведи себя в порядок, пока ещё кто-нибудь не увидел!</do>'
	end
	whoremday = day
end
if $args[0] = 'worry':
	*clr & cla
	$npc['37,worry_check'] = 0
	gs 'npc_editor','change_rep','-',37
	gs 'npc_editor','get_npc_profile',37
	gs 'zz_render','','','Мать не спит и стоит в коридоре, ожидая вас: <do>- Ты где шляешься?! Ты знаешь, какой час на дворе?!</do>'
	act 'В свою комнату': gt 'bedrPar'
end