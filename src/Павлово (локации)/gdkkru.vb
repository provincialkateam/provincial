frost = 0
minut += rand(3,7)
gs 'stat'
gs 'zz_funcs', 'colorize_day'
gs 'zz_render','Кружок кройки и шитья','pavlovo/dk/krupo.jpg','Вы зашли в кружок, где как раз шли занятия.'
act 'Уйти': gt 'gdk'
if krupo = 0:
	gs 'zz_render','','','Учительница, посмотрев на вас сказала, что кружок платный, стоит 3000 рублей за 30 занятий.'
	if money >= 3000:
		act 'Оплатить':
			money -= 3000
			krupo = 30
			gt $curloc
		end
	end
else
	if krupo_day = day: exit
	act 'Заниматься':
		cla
		act 'Уйти': gt 'gdk'
		if tkan > 0: gs 'zz_render','','','Вашей ткани хватит еще на <<tkan>> уроков.'
		if poSkill < 50:
			poSkill += rand(intel/10,intel/20)
			gs 'zz_render','','','Вы в течение часа учитесь теории швейного исскуства.'
			if poSkill > 40: gs 'zz_render','','','Учительница говорит вам, что для дальнейшего обучения понадобится ткань, которую можно приобрести в хозяйственном отделе супермаркета.'
		elseif poSkill >= 50 and poSkill < 100:
			if tkan = 0:
				gs 'zz_render','','','Учительница напоминает вам, что для дальнейшего совершенствования навыков необходимо практиковатся на ткани, которую нужно покупать самостоятельно.'
				exit
			else
				gs 'zz_render','','','В течение часа вы практикуетесь на купленной вами ткани, обучаясь кройке и шитью.'
			end
		elseif poSkill >= 100 and poSkill < 200:
			gs 'zz_render','','','Учительница с довольной улыбкой говорит вам, что теперь-то вы точно сможете перешивать одежду под свой размер. Но это не значит, что дальнейшие занятия бесполезны. Дальше вы можете научится вышивать гобелены, которые пользуются неплохим спросом, хотя и очень трудоёмки. Но зато весьма дороги.'
		else
			gs 'zz_render','','','Учительница довольно кивает и говорит вам, что теперь вы сможете вышить гобелен, пусть и простенький, и не без огрехов, но всё достигается практикой.'
		end
		if poSkill >= 50: tkan -= 1
		poSkill += rand(intel/10,intel/20)
		krupo -= 1
		minut += 60
		krupo_day = day
		gs 'stat'
	end
end